plugins {
    id "java-library"
    id "scala"
    id "com.timgroup.jarmangit"
    id "com.github.maiflai.scalatest"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = '0.0.' + buildNumber

repositories {
    maven {
        url = "$repoUrl/groups/public"
    }
}

// https://chrismarks.wordpress.com/2013/07/31/compiling-a-mixed-scala-project-with-gradle/
sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []
sourceSets.test.scala.srcDir "src/test/java"
sourceSets.test.java.srcDirs = []

dependencies {
    api project(":api-legacy")
    implementation project(":mysql")
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.8"
    compileOnly "org.joda:joda-convert:1.3.1"
    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "org.scala-lang:scala-library:$scalaVersion"

    testImplementation project(":api-legacy:testlib")
    testImplementation project(":memory-legacy")
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "com.timgroup:clocks-testing:1.0.1099" // autobump
    testImplementation "com.timgroup:clocks-joda-testing:1.0.1099" // autobump
    testImplementation "org.scalatest:scalatest_$scalaApiVersion:3.0.8"
    testCompileOnly "org.joda:joda-convert:1.3.1"
    testRuntimeOnly "org.pegdown:pegdown:1.4.2"
    testImplementation "mysql:mysql-connector-java:$mysqlJdbcVersion"
    testRuntimeOnly "org.slf4j:slf4j-simple:1.7.25"
}

check.dependsOn("scalatest")

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

tasks.withType(ScalaCompile).configureEach {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
    scalaCompileOptions.additionalParameters = ["-target:jvm-1.8"]
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

tasks.named("jar").configure {
    manifest {
        attributes(
                'Automatic-Module-Name': "com.timgroup.eventstore.mysql-legacy"
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

scalatest {
    maxParallelForks = 1
    dependsOn(":createDb")
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url = "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-mysql-legacy_${scalaApiVersion}"
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
