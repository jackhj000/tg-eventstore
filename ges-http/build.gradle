plugins {
    id "java-library"
    id "com.timgroup.jarmangit"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = '0.0.' + buildNumber

repositories {
    maven {
        url "${repoUrl}/groups/public"
    }
}

dependencies {
    api project(":api")
    compileOnly "com.google.code.findbugs:jsr305:1.3.9"
    implementation "org.apache.httpcomponents:httpclient:4.5.2"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.8"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.8"

    testImplementation project(":api:testlib")
    testImplementation project(":memory")
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "com.timgroup:clocks-testing:1.0.1102" // autobump
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

tasks.withType(Jar).configureEach {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

tasks.named("jar").configure {
    manifest {
        attributes(
                'Automatic-Module-Name': "com.timgroup.eventstore.ges-http"
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-ges-http"
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
