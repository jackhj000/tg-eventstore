plugins {
    id "java"
    id "com.timgroup.jarmangit"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = '0.0.' + buildNumber

repositories {
    maven {
        url "${repoUrl}/groups/public"
    }
}

dependencies {
    implementation project(":mysql")
    implementation "io.dropwizard.metrics:metrics-core:3.2.3"
    implementation "io.dropwizard.metrics:metrics-jvm:3.2.3"
    implementation "io.dropwizard.metrics:metrics-graphite:3.2.3"
    implementation project(":api")
    compileOnly "com.google.code.findbugs:jsr305:1.3.9"
    implementation "com.google.guava:guava:28.0-jre"
    runtimeOnly "mysql:mysql-connector-java:$mysqlJdbcVersion"
    testImplementation project(":api:testlib")
    testImplementation project(":memory")

    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "com.timgroup:clocks-testing:1.0.1096" // autobump
}

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

tasks.withType(Jar).all {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-diffing"
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
