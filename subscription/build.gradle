plugins {
    id "java-library"
    id "com.timgroup.jarmangit"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = '0.0.' + buildNumber

repositories {
    maven {
        url "${repoUrl}/groups/public"
    }
}

dependencies {
    api project(":api")
    compileOnly "com.google.code.findbugs:jsr305:1.3.9"
    implementation "com.lmax:disruptor:3.3.2"
    implementation "com.timgroup:tim-structured-events:0.4.1260" // autobump
    implementation "com.google.guava:guava:27.0.1-jre"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.8"
    implementation "org.slf4j:slf4j-api:1.7.25"

    testImplementation project(":api:testlib")
    testImplementation project(":memory")
    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation "com.timgroup:clocks-testing:1.0.1091" // autobump
    testImplementation "com.timgroup:tim-structured-events-testing:0.4.1260" // autobump
    testImplementation "org.mockito:mockito-core:2.26.0"
    testImplementation "com.youdevise:Matchers:0.0.1287" // autobump
    testRuntimeOnly "org.slf4j:slf4j-simple:1.7.25"
}

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

tasks.withType(Jar).all {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-subscription"
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
